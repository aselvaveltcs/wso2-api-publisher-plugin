plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'com.github.ben-manes.versions' version '0.14.0'
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven { url 'https://repo.gradle.org/gradle/repo' }
    mavenCentral()
}

dependencies {
    compile gradleApi()

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.9'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'com.github.michaelruocco', name: 'wso2-api-manager-client', version: '4.0.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.2'
    testCompile group: 'eu.codearte.catch-exception', name: 'catch-exception', version: '1.4.4'
    testCompile group: 'com.github.michaelruocco', name: "wso2-test-containers", version: '1.0.1'
}

tasks.withType(Test) {
    systemProperty "javax.net.ssl.trustStore", "$projectDir/truststore/cacerts"
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(uploadArchives)) {
        signing {
            required true
            sign configurations.archives
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                packaging 'jar'
                name 'WSO2 API Publisher Plugin'
                description 'Gradle plugin that enables WSO2 API publishing'
                groupId 'com.github.michaelruocco'
                artifactId 'wso2-api-publisher-plugin'
                version '1.0.0'
                url 'https://github.com/michaelruocco/wso2-api-publisher-plugin'

                developers {
                    developer {
                        id 'mruoc'
                        name 'Michael Ruocco'
                        email 'michael.ruocco@hotmail.com'
                    }
                }

                scm {
                    connection 'scm:git:git@github.com/michaelruocco/wso2-api-publisher-plugin.git'
                    developerConnection 'scm:git:git@github.com/michaelruocco/wso2-api-publisher-plugin.git'
                    url 'git@github.com/michaelruocco/wso2-api-publisher-plugin.git'
                }

                licenses {
                    license {
                        name 'BSD License'
                        url 'http://www.opensource.org/licenses/bsd-license.php'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}